route change console select_console
#
# Expects shared data 
#    player_list that has the list of player ships
#    player_count the number of expected player ships
#    game_stared game state
#

========= client_main ==========
console = "helm"
client_select_ship = "artemis"

========== select_console ==========

ship_list = ""
i = 0
for player_ship in player_list:
    if i>=player_count:
        break
    end_if
    i += 1
    if len(ship_list) >0:
        ship_list = ship_list + ","
    end_if
    ship_list = ship_list + player_ship.name
next player_ship

#section style="area: 10,50, 35,90;row-height: 50px;"
# button "Debug Mast":
#    jump debug_mast
# end_button

section style="area: 60,50, 75,90;row-height: 50px;"
vradio client_select_ship "{ship_list}"

section style="area: 85,50, 99,90;"
vradio console_select "helm,weapons, comms,science,engineering"
blank
row

if not game_started:
    checkbox console_ready "ready"
else:
    button "ready":
        jump console_selected
    end_button
end_if

#
# Redraw if player count changes
#
on change player_count:
    jump select_console
end_on

await gui


# Multiple labels to call to get here

====== game_started_console ====
====== console_selected ====

has_ship= False
for player_ship in to_object_list(role("__PLAYER__")):
    if player_ship.name == client_select_ship:
        has_ship = True
        do sbs.assign_client_to_ship(client_id, player_ship.id)
        do set_inventory_value(client_id, "assigned_ship", player_ship.id)
    end_if
next player_ship

if not has_ship:
    jump select_console
end_if
====== show_console_selected ====
#
# Make it so the console can be found if needed
#
do set_inventory_value(client_id, "CONSOLE_TYPE", console_select)
console console_select

#
#
# Use a label replace to override: 
#   ==== replace: console_bottom_choices ====
#        You code
#
==== console_bottom_choices ====
await gui




=========== debug_mast =========

task_count = 0
for sch in mast.schedulers:
    for tsk in sch.tasks:
        task_count += 1
    next tsk
next sch

section style="area: 0,50px, 100,100;"
"""""
Task count {task_count}
""""""
await gui timeout 4s

jump debug_mast