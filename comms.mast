route comms select route_comms

#############################################
# Create task to player to raider comms and station comms
#############################################

=========== route_comms  ========
# start the comms for the players and stations
# Each ship will have its of thread for comms
# this enables them to have a unique path

if COMMS_SELECTED_ID == COMMS_ORIGIN_ID:
    # This is the same ship
    jump internal_comms
elif has_roles(COMMS_SELECTED_ID, "tsn, Station"):
    torpedo_build_type = sbs.TORPEDO.HOMING
    jump station_comms
elif has_roles(COMMS_SELECTED_ID, "tsn, friendly"):
    jump friendly_comms
elif has_roles(COMMS_SELECTED_ID, 'Station'):
    jump comms_enemy_station
elif has_role(COMMS_SELECTED_ID, "Raider"):
    jump npc_comms
end_if

->END

===== friendly_comms =======

await comms:
    + "Give Orders":
        comms_client_id = EVENT.client_id
        reroute client comms_client_id friendly_give_orders
end_await
jump  friendly_comms


=============== station_comms ===============
comms_id = to_object(COMMS_ORIGIN_ID).comms_id
self = to_object(COMMS_SELECTED_ID)

await comms:
    + "Hail":
        homing = self.get_engine_data(sim, "torpedo_count", sbs.TORPEDO.HOMING)
        nuke = self.get_engine_data(sim, "torpedo_count", sbs.TORPEDO.NUKE)
        emp = self.get_engine_data(sim, "torpedo_count", sbs.TORPEDO.EMP)
        mine = self.get_engine_data(sim, "torpedo_count", sbs.TORPEDO.MINE)
        receive """Hello, {comms_id}.  We stand ready to assist.
You have full docking privileges.
{homing} Homing ready
{nuke} Nuke ready
{emp} EMP ready
{mine} Mine ready
{torpedo_build_type} in production.
"""
    + "Now Docking":
        receive  """We read you, {comms_id}.  We're standing by for expedited docking.^"""

    + "Hello, world":
        receive  """Hello, World"""

    + "Build Homing": 
        receive  """We read you, {comms_id}.  We will focus on homing production.^"""
        torpedo_build_type = sbs.TORPEDO.HOMING
        cancel build_task
        var build_task =>  task_station_building

    + "Build Nuke":
        receive  """We read you, {comms_id}.  We will focus on nuke production.^"""
        torpedo_build_type= sbs.TORPEDO.NUKE
        cancel build_task
        var build_task => task_station_building

    + "Build Emp":
        receive  """We read you, {comms_id}.  We will focus on EMP production.^"""
        torpedo_build_type= sbs.TORPEDO.EMP
        cancel build_task
        var build_task => task_station_building
    + "Build Mine":
        receive  """We read you, {comms_id}.  We will focus on MINE production.^"""
        torpedo_build_type = sbs.TORPEDO.MINE
        cancel build_task
        var build_task => task_station_building
end_await
-> station_comms

=== task_station_building ===
delay sim 10s
~~
cur_count = self.get_engine_data(sim, "torpedo_count", torpedo_build_type)
self.set_engine_data(sim, "torpedo_count", cur_count+1, torpedo_build_type)
~~
receive  """{comms_id}. {torpedo_build_type} Production complete."""
->task_station_building


======== comms_enemy_station ====== 
comms_id = to_object(COMMS_ORIGIN_ID).comms_id
await comms:
    + "Hail":
        receive "{comms_id}! We will destroy you, disgusting Terran scum!"
    + "You're Ugly":
        receive  """You are a foolish Terran, {comms_id}.  We know that the taunt functionality is not currently implemented.^"""
    + "Surrender now":
        receive """OK we give up, {comms_id}."""
end_await

-> comms_enemy_station


================ npc_comms ==================
comms_id = to_object(COMMS_ORIGIN_ID).comms_id
enrage_value=get_inventory_value(COMMS_SELECTED_ID, "enrage_value")
hide_butt = has_role(COMMS_SELECTED_ID, "never_surrender") or has_role(COMMS_SELECTED_ID, "surrendered")
await comms:
    + "Hail":
        receive "{comms_id}! Make your time!"
    + "Taunt" if enrage_value is None and not hide_butt:
        # Navigate to sub Menu
        jump comms_taunts
    + "Surrender now" if not hide_butt:
        blob = get_engine_data_set(sim, COMMS_SELECTED_ID)
        shield_count = blob.get("shield_count", 0)
        s_ratio = 100
        for s in range(shield_count):
            s_max = blob.get("shield_max_val", s )
            s_cur = blob.get("shield_val", s )
            s_ratio = min(s_cur/s_max, s_ratio)
        next s
        if s_ratio < 0.09:
            if random.randint(1,6)<3:
                receive """OK we give up, {comms_id}."""
                do add_role(COMMS_SELECTED_ID, "surrendered")
                do remove_role(COMMS_SELECTED_ID, "raider")
                do set_engine_data(COMMS_SELECTED_ID, sim, "surrender_flag", 1)
            else:
                receive """We will fight to our last breath!"""
                do add_role(COMMS_SELECTED_ID, "never_surrender")
            end_if
        elif s_ratio < 0.5:
            if random.randint(0,6)<=2:
                receive """OK we give up, {comms_id}."""
                do add_role(COMMS_SELECTED_ID, "surrendered")
                do remove_role(COMMS_SELECTED_ID, "raider")
                do set_engine_data(COMMS_SELECTED_ID, sim, "surrender_flag", 1)
            else:
                receive """We can still defeat you, {comms_id}! Prepare to die!"""
            end_if
        else:
            receive """Go climb a tree, {comms_id}!"""
        end_if
        
end_await
jump npc_comms



================ comms_taunts ==================
enemy_side = to_object(COMMS_SELECTED_ID).side

match enemy_side:
    case "kralien":
        jump kralien_comms_taunts
 case "arvonian":
        jump arvonian_comms_taunts
case "torgoth":
        jump torgoth_comms_taunts
case "skaraan":
        jump skaraan_comms_taunts

end_match
# No Taunt for you
jump npc_comms 

================ kralien_comms_taunts ==================
name = to_object(COMMS_ORIGIN_ID).name
taunt_trait=get_inventory_value(COMMS_SELECTED_ID, "taunt_trait")
enrage_value=0
await comms:
    + "Your ship is a rusting bucket of table scraps.":
        # failure
        if taunt_trait!=0:
        receive  """And your ship is stupid and ugly, {name}."""
        # success
        else:
        receive  """Rust you say? Prepare to meet the gods, Terran infidel!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "The deadliest weapon on that pathetic ship is your face!":
        # failure
        if taunt_trait!=1:
        receive  """And your face is soft like a dung heap {name}."""
        # success
        else:
        receive  """Your face will be shredded by these weapons!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "Hey wormface! I'm wiping my stinky feet your Holy Scrolls!":
        # failure
        if taunt_trait!=2:
        receive  """Stick it up your asteroid, {name}. This war will be lost by you."""
        # success
        else:
        receive  """You shall die immediately for your blasphemy!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
end_await
do set_inventory_value(COMMS_SELECTED_ID, "enrage_value",(enrage_value, COMMS_ORIGIN_ID))
# Jump back to main comms menu
-> npc_comms

================ arvonian_comms_taunts ==================
name = to_object(COMMS_ORIGIN_ID).name
taunt_trait=get_inventory_value(COMMS_SELECTED_ID, "taunt_trait")
enrage_value=0
name = to_object(COMMS_ORIGIN_ID).name
await comms:
    + "Your uniform is awful. You look like you mugged a jellyfish.":
        # failure
        if taunt_trait!=0:
        receive  """That just shows you are a tasteless dimwit, {name}."""
         # success
         else:
        receive  """I shall destroy your hideous hide, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "Your wart-faced King and Queen smell like beetle dung!":
         # failure
         if taunt_trait!=1:
        receive  """And your ghastly Terran planet reeks of greasy food and cheap wine, {name}."""
         # success
         else:
        receive  """I shall avenge your insult in the name of the Queen, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "Your Supreme Understander is a tin-boxed wind-up toy!":
         # failure
         if taunt_trait!=2:
        receive  """Perhaps, {name}, but it's still smarter than you."""
         # success
         else:
        receive  """Our Supreme Understander has decreed that you must die now, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
end_await
do set_inventory_value(COMMS_SELECTED_ID, "enrage_value",(enrage_value, COMMS_ORIGIN_ID))
# Jump back to main comms menu
-> npc_comms

================ torgoth_comms_taunts ==================
name = to_object(COMMS_ORIGIN_ID).name
taunt_trait=get_inventory_value(COMMS_SELECTED_ID, "taunt_trait")
enrage_value=0
name = to_object(COMMS_ORIGIN_ID).name
await comms:
    + "Hey Jumbo! Your mother is a Space Whale!":
        # failure
        if taunt_trait!=0:
        receive  """So is your father, {name}."""
        # success
        else:
        receive  """You shall die for insulting my mother, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "You are so ugly your spouse will thank me for killing you.":
        # failure
        if taunt_trait!=1:
        receive  """I'm not married, excrement face."""
        # success
        else:
        receive  """I shall crush your skull with my bare hands for insulting my spouse, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "Take a bath, you fungus-infested pachyderm!":
        # failure
        if taunt_trait!=2:
        receive  """Then I am the last thing the Terrans will smell as I destroy all your ships and bases."""
        # success
        else:
        receive  """How dare you insult my hygiene? Prepare to die, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
end_await
do set_inventory_value(COMMS_SELECTED_ID, "enrage_value",(enrage_value, COMMS_ORIGIN_ID))
# Jump back to main comms menu
-> npc_comms


================ skaraan_comms_taunts ==================
name = to_object(COMMS_ORIGIN_ID).name
taunt_trait=get_inventory_value(COMMS_SELECTED_ID, "taunt_trait")
enrage_value=0
name = to_object(COMMS_ORIGIN_ID).name
await comms:
    + "Hey Skaraan! Your ship is so slow it leaves a slime trail!":
        # failure
        if taunt_trait!=0:
        receive  """So does your mother, {name}."""
        # success
        else:
        receive  """I have the fastest ship in the galaxy, {name}. And it's about to blow you to smithereens!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "Penniless pauper! Your portfolio sucks like a black hole!":
            # failure
            if taunt_trait!=1:
        receive  """I could buy and sell your worthless crew and squalid ship, {name}."""
        # success
        else:
        receive  """How dare you insult my finances, {name}? I shall enjoy earning the bonus for killing you!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
    + "Your mommy raised you to be a worthless flunkey!":
        # failure
        if taunt_trait!=2:
        receive  """Whatever you say, {name}. Right now I'm enjoying the company of YOUR mommy."""
        # success
        else:
        receive  """How dare you insult my status? I shall mount your lifeless head on my wall as a trophie, {name}!"""
        enrage_value=sim.time_tick_counter+30*120
        end_if
end_await
do set_inventory_value(COMMS_SELECTED_ID, "enrage_value",(enrage_value, COMMS_ORIGIN_ID))
# Jump back to main comms menu
-> npc_comms

================ internal_comms ==================
#
# Setup faces for the departments
#
doctor = random_terran()
biologist = random_terran()
counselor = random_terran()
major = random_terran()
sec = "Security"

================ internal_comms_loop ==================
#
# Shows button color, face and title overrides
#
await comms:
    + "Sickbay" color "blue":
        receive "The crew health is great!" title "sickbay" face "{doctor}" color "blue"
    + "Security" color "red":
        receive  "All secure" title sec face major color "red"
    + "Exobiology" color "green":
        receive  "Testing running, one moment" title "Exobiology" face biologist color "green"
        # It is best to schedule delayed responses so the comms buttons are not stalled
        schedule test_finished
    + "counselor" color "cyan":
        receive  "Something is disturbing the crew" title "counselor" face counselor color "cyan"
        #
        # but you can delay comms, There will be no buttons during this delay
        #
        delay sim 3s
        receive  "Things feel like they are getting worse" title "counselor" face counselor color "cyan"
end_await
-> internal_comms_loop

====== test_finished ======
# This is a new task to delay the response of the test results
# COMMS variables should migrate to this task

delay sim 2s
receive  "test results finished. Tell the captain we have a pathogen. This could be bad." title "Exobiology" face biologist color "green"
->END

